let
  extern print : (String) -> {} = "print"
  val n = 8
  val row = array(0, n)
  val col = array(0, n)
  val diag1 = array(0, n + n - 1)
  val diag2 = array(0, n + n - 1)

  fun printboard() =
    let fun loopi(i) =
      if i < n
      then let fun loopj(j) = 
        if j < n
        then (print(if col[i] == j then " O" else " ."); loopj(j + 1))
        else print("\n")
        in loopj(0); loopi(i + 1)
      else print("\n")
    in loopi(0)
  
  fun try(c) =
    let fun loop(r) =
      if r < n
      then if row[r] == 0 && diag1[r + c] == 0 && diag2[r + 7 - c] == 0
           then (row[r] := 1; diag1[r + c] := 1; diag2[r + 7 - c] := 1;
                 col[c] := r;
                 try(c + 1);
                 row[r] := 0; diag1[r + c] := 0; diag2[r + 7 - c] := 0;
                 loop(r + 1))
           else loop(r + 1)
      else {}
    in if c == n
       then printboard()
       else loop(0)
in try(0)
  